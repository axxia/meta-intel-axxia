#@TYPE: Machine
#@NAME: axxiax86-64
#@DESCRIPTION: Machine configuration for INTEL Axxia x86_64 systems (Intel Snowridge CPU) with MMX, SSE, SSE2, SSE3, SSSE3, SSE4.1 and SSE4.2 instruction set support. Supports a moderately wide range of drivers that should boot and be usable on "typical" hardware.

PREFERRED_PROVIDER_virtual/kernel ?= "linux-intel"
PREFERRED_VERSION_linux-intel ?= "4.19%"
PREFERRED_VERSION_linux-intel-rt ?= "4.19%"

require conf/machine/include/meta-intel.inc
require conf/machine/include/tune-snr.inc
require conf/machine/include/intel-common-pkgarch.inc

MACHINE_FEATURES += "pcbios efi"
MACHINE_FEATURES += "wifi 3g nfc"
MACHINE_FEATURES += "intel-ucode ext3"
MACHINE_FEATURES_BACKFILL_CONSIDERED = "qemu-usermode"

KARCH = "x86_64"
KERNEL_IMAGETYPE = "bzImage"
KEEPUIMAGE = "no"

EFI_PROVIDER ?= "grub-efi"

SYSLINUX_OPTS = "serial 0 115200"
SERIAL_CONSOLES = "115200;ttyS4"

SNR_SERIAL_CONSOLE ?= "ttyS4"
APPEND = "console=tty0 console=${SNR_SERIAL_CONSOLE},115200 intel_iommu=on idle=poll"
GRUB_SERIAL = "console=${SNR_SERIAL_CONSOLE},115200"

DEFAULTTUNE_virtclass-multilib-lib32 = "snr-32"
MULTILIBS = "multilib:lib32"

IMAGE_FSTYPES += " ext2"
IMAGE_FSTYPES += " tar.gz"
IMAGE_FSTYPES += " iso"
NOISO = "0"

IMAGE_FSTYPES += "wic"
WKS_FILE = "${@bb.utils.contains_any("EFI_PROVIDER", "systemd-boot", \
	   "systemd-bootdisk-microcode-${SNR_SERIAL_CONSOLE}.wks", \
	   "grub-bootdisk-microcode-${SNR_SERIAL_CONSOLE}.wks", d)}"
WKS_FILE_DEPENDS_append = " intel-microcode"
