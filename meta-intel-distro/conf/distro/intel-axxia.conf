require conf/distro/poky.conf \
	${@bb.utils.contains('DISTRO_FEATURES', 'multilib', \
	'conf/multilib.conf', '', d)} \
	${@bb.utils.contains('DISTRO_FEATURES', 'qsp', \
	'conf/distro/include/intel-axxia-qsp.inc', '', d)} \
	${@bb.utils.contains('DISTRO_FEATURES', 'gcc11', \
	'conf/distro/include/intel-axxia-gcc11.inc', '', d)}

DISTRO = "intel-axxia"
DISTRO_NAME = "Intel Axxia Distro"
DISTRO_VERSION = "${RELEASE_VERSION}"
DISTRO_CODENAME = "intel-axxia"
SDK_VENDOR = "-intelaxxiasdk"
SDK_VERSION := "${RELEASE_VERSION}"

TARGET_VENDOR = "-intelaxxia"
RELEASE_VERSION ?= "grr_deliveryXX_linuxX_XX"

DISTRO_FEATURES:append = " pam virtualization kvm security tpm"

include conf/distro/include/intel-axxia-defaults.inc
include conf/distro/include/intel-axxia-custom.inc
include conf/distro/include/intel-axxia-flags.inc

# Enable SIMICS support and set the version to compile
#DISTRO_FEATURES:append = " simics"
#SIMICS_VERSION = ""

# Choose GCC version: 15.% (gcc15) or 11.% (gcc11). Default version: 13.%
#DISTRO_FEATURES:append = " gcc15"

BBMASK:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'simics', '', \
			'meta-intel-distro/recipes-support/simics', d)} \
		  ${@oe.utils.conditional('ALTERNATIVE_KERNELS', '', \
			'meta-intel-distro/recipes-kernel/lttng/lttng-modules-alternative*', '', d)} \
		  "
