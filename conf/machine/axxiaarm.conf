#@TYPE: Machine
#@NAME: axxiaarm
#@DESCRIPTION: Machine configuration for INTEL Axxia ARM systems

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-rt"
PREFERRED_VERSION_linux-yocto ?= "4.9%"

MACHINE_FEATURES += "ext3"

# Default tune will be for 5500 board: cortexa17 (armv7a)
CHIPSET ?= "5500"
TUNE_X9 = "conf/machine/include/tune-cortexa57.inc"
TUNE_XLF = "conf/machine/include/tune-cortexa53.inc"
TUNE_5500 = "conf/machine/include/tune-cortexa15.inc"
require ${TUNE_${CHIPSET}}

# Armv8a SoCs supports 32-bit and 64-bit states.
# Set default to 32-bit state for axxiarm machine
DEFAULTTUNE_X9 = "cortexa57_32-crypto"
DEFAULTTUNE_XLF = "cortexa53_32-crypto"
DEFAULTTUNE_5500 = "cortexa15-neon"
DEFAULTTUNE ?= "${@bb.utils.contains('CHIPSET', 'X9', '${DEFAULTTUNE_X9}', \
		bb.utils.contains('CHIPSET', 'XLF', '${DEFAULTTUNE_XLF}', \
		'${DEFAULTTUNE_5500}', d), d)}"

KARCH = "arm"
KERNEL_IMAGETYPE = "zImage"
UBOOT_ENTRYPOINT = "0x400000"
KEEPUIMAGE = "no"

IMAGE_FSTYPES += "ext2"
IMAGE_FSTYPES += "tar.gz"

SERIAL_CONSOLE = "9600 ttyAMA0"
