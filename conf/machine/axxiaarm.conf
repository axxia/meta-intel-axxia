#@TYPE: Machine
#@NAME: axxiaarm
#@DESCRIPTION: Machine configuration for INTEL Axxia ARM systems

PREFERRED_VERSION_linux-yocto ?= "4.1%"
#PREFERRED_VERSION_u-boot-axxia ?= "2013.01.01%"
PREFERRED_VERSION_nfs-utils ?= "1.3.1"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
#PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-axxia"
#EXTRA_IMAGEDEPENDS = "u-boot-axxia"
PREFERRED_VERSION_lxc ?= "2.0.6"

MACHINE_FEATURES += "ext3"

MACHINE_EXTRA_RRECOMMENDS += ""

# Default tune will be for 5500 board: cortexa17 (armv7a)
CHIPSET ?= "5500"
TUNE_X9 = "conf/machine/include/tune-cortexa57.inc"
TUNE_XLF = "conf/machine/include/tune-cortexa53.inc"
TUNE_5500 = "conf/machine/include/tune-cortexa15.inc"
require ${TUNE_${CHIPSET}}

# Armv8a SoCs supports 32-bit and 64-bit states. Set default to 32-bit state for axxiarm machine
DEFAULTTUNE_X9 = "cortexa57_32-crypto"
DEFAULTTUNE_XLF = "cortexa53_32-crypto"
DEFAULTTUNE_5500 = "cortexa15-neon"
DEFAULTTUNE ?= "${@bb.utils.contains('CHIPSET', 'X9', '${DEFAULTTUNE_X9}', \
	          bb.utils.contains('CHIPSET', 'XLF', '${DEFAULTTUNE_XLF}', '${DEFAULTTUNE_5500}', d), d)}"

KARCH = "arm"
KERNEL_IMAGETYPE = "zImage"
UBOOT_ENTRYPOINT = "0x400000"
KEEPUIMAGE = "no"

IMAGE_FSTYPES += "ext2"
IMAGE_FSTYPES += "tar.gz"

SERIAL_CONSOLE = "9600 ttyAMA0"

# For axxia kernels skip bbappend from meta-virtualization which add kernel fragments
BBMASK = "/meta-virtualization/recipes-kernel/linux/"
